plugins {
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'java'
}

repositories {
	mavenCentral()
}

configurations {
    asciidoctorExtensions
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    runtimeOnly('org.hsqldb:hsqldb')
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit;'
    }
    testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc')
    asciidoctorExtensions('org.springframework.restdocs:spring-restdocs-asciidoctor')
}

ext {
    snippetsDir = file('build/generated-snippets')
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

tasks.named('asciidoctor') {
    configurations 'asciidoctorExtensions'
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    outputDir 'build/asciidoc'
    dependsOn test
}

tasks.named('jar') {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static'
    }
    archiveBaseName = 'wj'
    archiveVersion = '0.0.1-SNAPSHOT'
}